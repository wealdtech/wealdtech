/*
 * Copyright 2012 - 2014 Weald Technology Trading Limited
 *
 * Licensed under the Apache License, Version 2.0 (the "License"); you may not use this file except in compliance with the License.  You may obtain a copy of the License at
 *
 *   http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software distributed under the License is distributed on an "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.  See the License for the specific language governing permissions and limitations under the License.
 */

subprojects {
    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'maven'
    apply plugin: 'signing'

    repositories {
        mavenLocal()
        mavenCentral()
    }

    dependencies {
        // @Nullable and @NotNull annotations
        compile 'com.google.code.findbugs:jsr305:3.0.0'

        testCompile 'org.testng:testng:6.+'
    }

    group = 'com.wealdtech'
    version = '3.0.0'

  configurations.all {
    resolutionStrategy {
      force 'edu.stanford.nlp:stanford-corenlp:3.6.0'
    }
  }

    test {
        useTestNG()
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from 'build/docs/javadoc'
    }

    task sourcesJar(type: Jar) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    sourceCompatibility = 1.7
    targetCompatibility = 1.7

    javadoc {  
        failOnError = false  
    }

    artifacts {
        archives jar
        archives javadocJar
        archives sourcesJar
    }

    signing {
        sign configurations.archives
    }
    signArchives.onlyIf { gradle.taskGraph.hasTask(uploadArchives) }

    if (!project.hasProperty('sonatypeUsername')) {
        ext.sonatypeUsername = ''
    }
    if (!project.hasProperty('sonatypePassword')) {
        ext.sonatypePassword = ''
    }
    uploadArchives {
        repositories {
            mavenDeployer {
                beforeDeployment {
                    MavenDeployment deployment -> signing.signPom(deployment)
                }

                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                    authentication(userName: sonatypeUsername, password: sonatypePassword)
                }

                pom.project {
                    packaging 'jar'
                    url 'https://github.com/wealdtech/wealdtech'

                    scm {
                        url 'scm:git@github.com:wealdtech/wealdtech.git'
                        connection 'scm:git@github.com:wealdtech/wealdtech.git'
                        developerConnection 'scm:git@github.com:wealdtech/wealdtech.git'
                    }

                    licenses {
                        license {
                            name 'The Apache Software License, Version 2.0'
                            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                            distribution 'repo'
                        }
                    }

                    developers {
                        developer {
                            id 'wealdtech'
                            name 'Weald Technology'
                        }
                    }
                }
            }
        }
    }

    idea.module.iml {
        beforeMerged { module ->
            module.dependencies.clear()
        }
    }
}
